apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.objectbox'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId 'closer.vlllage.com.closer'
        minSdkVersion 24
        targetSdkVersion 29
        versionCode readVersionCode()
        versionName '2.1'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }
    signingConfigs {
        release {
            storeFile file("../../../android.jks")
            storePassword "closertome"
            keyAlias "closer"
            keyPassword "closertome"
            v1SigningEnabled true
            v2SigningEnabled true
        }
        debug {
            storeFile file("../../../android.jks")
            storePassword "closertome"
            keyAlias "closer"
            keyPassword "closertome"
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

play {
    serviceAccountCredentials = file('../../../service-account-key.json')
    track = 'internal'
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation ('androidx.constraintlayout:constraintlayout:2.0.4') {
        exclude group: 'com.android.support'
    }
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'com.google.android.material:material:1.3.0-alpha03'
    implementation ('com.google.android.gms:play-services-maps:17.0.0') {
        exclude group: 'com.android.support'
    }
    implementation ('com.google.android.gms:play-services-location:17.0.0') {
        exclude group: 'com.android.support'
    }
    implementation group: 'com.google.maps.android', name: 'android-maps-utils', version: '0.6.1'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.18'
    implementation 'com.google.firebase:firebase-core:17.5.0'
    implementation 'com.google.firebase:firebase-messaging:20.2.4'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'jp.wasabeef:glide-transformations:4.1.0'
    implementation group: 'com.journeyapps', name: 'zxing-android-embedded', version: '4.1.0'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.4'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
    implementation ('com.github.chrisbanes:PhotoView:2.3.0') {
        exclude group: 'com.android.support'
    }
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.9.2'
    implementation 'com.vdurmont:emoji-java:5.1.1'
    implementation 'com.luckycatlabs:SunriseSunsetCalculator:1.2'
    implementation 'com.github.Queatz:Android-UX-Extras-Slide-Screen:0.2'
    implementation 'com.github.Queatz:On:0.1.6.4'
    implementation group: 'com.github.haifengl', name: 'smile-core', version: '1.5.2'
    implementation "io.objectbox:objectbox-kotlin:$objectboxVersion"
    implementation "io.objectbox:objectbox-rxjava:$objectboxVersion"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    implementation("org.webrtc:google-webrtc:$webrtcVersion")
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

def readVersionCode() {
    def versionPropsFile = file('../version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        return versionProps['versionCode'].toInteger()
    }

    throw new GradleException("Could not read version.properties!")
}

def incrementVersionCode() {
    def versionPropsFile = file('../version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        def code = versionProps['versionCode'].toInteger() + 1
        versionProps['versionCode'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
        return code
    }

    throw new GradleException("Could not read version.properties!")
}

task prepareVersionCode doLast {
    print 'Version code updated to ' + incrementVersionCode()
}

tasks.whenTaskAdded { task ->
    if (task.name == 'publishReleaseBundle') {
        task.dependsOn prepareVersionCode
    }
}

repositories {
    mavenCentral()
}